// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

import "./libraries/EthRejector.sol";
import "./libraries/Permitable.sol";

contract UniswapV2Exchange is EthRejector, Permitable {
    uint256 private constant TRANSFER_FROM_CALL_SELECTOR_32 = 0x23b872dd00000000000000000000000000000000000000000000000000000000;
    uint256 private constant WETH_DEPOSIT_CALL_SELECTOR_32 = 0xd0e30db000000000000000000000000000000000000000000000000000000000;
    uint256 private constant WETH_WITHDRAW_CALL_SELECTOR_32 = 0x2e1a7d4d00000000000000000000000000000000000000000000000000000000;
    uint256 private constant ERC20_TRANSFER_CALL_SELECTOR_32 = 0xa9059cbb00000000000000000000000000000000000000000000000000000000;
    uint256 private constant ADDRESS_MASK = 0x000000000000000000000000ffffffffffffffffffffffffffffffffffffffff;
    uint256 private constant REVERSE_MASK = 0x8000000000000000000000000000000000000000000000000000000000000000;
    uint256 private constant WETH_MASK = 0x4000000000000000000000000000000000000000000000000000000000000000;
    uint256 private constant NUMERATOR_MASK = 0x0000000000000000ffffffff0000000000000000000000000000000000000000;
    uint256 private constant WETH = 0x000000000000000000000000C02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
    uint256 private constant UNISWAP_PAIR_RESERVES_CALL_SELECTOR_32 =
        0x0902f1ac00000000000000000000000000000000000000000000000000000000;
    uint256 private constant UNISWAP_PAIR_SWAP_CALL_SELECTOR_32 =
        0x022c0d9f00000000000000000000000000000000000000000000000000000000;
    uint256 private constant DENOMINATOR = 1000000000;
    uint256 private constant NUMERATOR_OFFSET = 160;

    function callUniswapToWithPermit(
        IERC20 srcToken,
        uint256 amount,
        uint256 minReturn,
        bytes32[] calldata pools,
        bytes calldata permit,
        address payable recipient
    ) external returns (uint256 returnAmount) {
        bool claimed = _permit(address(srcToken), permit);
        return _callUniswap(srcToken, amount, minReturn, pools, recipient, claimed);
    }

    function callUniswapWithPermit(
        IERC20 srcToken,
        uint256 amount,
        uint256 minReturn,
        bytes32[] calldata pools,
        bytes calldata permit
    ) external returns (uint256 returnAmount) {
        bool claimed = _permit(address(srcToken), permit);
        return _callUniswap(srcToken, amount, minReturn, pools, payable(msg.sender), claimed);
    }

    function callUniswapTo(
        IERC20 srcToken,
        uint256 amount,
        uint256 minReturn,
        bytes32[] calldata pools,
        address payable recipient
    ) external payable returns (uint256 returnAmount) {
        return _callUniswap(srcToken, amount, minReturn, pools, recipient, false);
    }

    function callUniswap(
        IERC20 srcToken,
        uint256 amount,
        uint256 minReturn,
        bytes32[] calldata pools
    ) external payable returns (uint256 returnAmount) {
        return _callUniswap(srcToken, amount, minReturn, pools, payable(msg.sender), false);
    }

    function _callUniswap(
        IERC20 srcToken,
        uint256 amount,
        uint256 minReturn,
        bytes32[] calldata /* pools */,
        address payable recipient,
        bool claimed
    ) internal returns (uint256 returnAmount) {
        assembly {
            // solhint-disable-line no-inline-assembly
            function reRevert() {
                returndatacopy(0, 0, returndatasize())
                revert(0, returndatasize())
            }

            function revertWithReason(m, len) {
                mstore(0, 0x08c379a000000000000000000000000000000000000000000000000000000000)
                mstore(0x20, 0x0000002000000000000000000000000000000000000000000000000000000000)
                mstore(0x40, m)
                revert(0, len)
            }

            function swap(emptyPtr, swapAmount, pair, reversed, numerator, dst) -> ret {
                mstore(emptyPtr, UNISWAP_PAIR_RESERVES_CALL_SELECTOR_32)
                if iszero(staticcall(gas(), pair, emptyPtr, 0x4, emptyPtr, 0x40)) {
                    reRevert()
                }

                let reserve0 := mload(emptyPtr)
                let reserve1 := mload(add(emptyPtr, 0x20))
                if reversed {
                    let tmp := reserve0
                    reserve0 := reserve1
                    reserve1 := tmp
                }
                ret := mul(swapAmount, numerator)
                ret := div(mul(ret, reserve1), add(ret, mul(reserve0, DENOMINATOR)))

                mstore(emptyPtr, UNISWAP_PAIR_SWAP_CALL_SELECTOR_32)
                switch reversed
                case 0 {
                    mstore(add(emptyPtr, 0x04), 0)
                    mstore(add(emptyPtr, 0x24), ret)
                }
                default {
                    mstore(add(emptyPtr, 0x04), ret)
                    mstore(add(emptyPtr, 0x24), 0)
                }
                mstore(add(emptyPtr, 0x44), dst)
                mstore(add(emptyPtr, 0x64), 0x80)
                mstore(add(emptyPtr, 0x84), 0)
                if iszero(call(gas(), pair, 0, emptyPtr, 0xa4, 0, 0)) {
                    reRevert()
                }
            }

            function callSwap(emptyPtr, token, srcAmount, swapCaller, receiver, min, claim) -> ret {
                let poolsOffset := add(calldataload(0x64), 0x4)
                let poolsEndOffset := calldataload(poolsOffset)
                poolsOffset := add(poolsOffset, 0x20)
                poolsEndOffset := add(poolsOffset, mul(0x20, poolsEndOffset))
                let rawPair := calldataload(poolsOffset)
                switch token
                case 0 {
                    if iszero(eq(srcAmount, callvalue())) {
                        revertWithReason(0x00000011696e76616c6964206d73672e76616c75650000000000000000000000, 0x55) // "invalid msg.value"
                    }

                    mstore(emptyPtr, WETH_DEPOSIT_CALL_SELECTOR_32)
                    if iszero(call(gas(), WETH, srcAmount, emptyPtr, 0x4, 0, 0)) {
                        reRevert()
                    }

                    mstore(emptyPtr, ERC20_TRANSFER_CALL_SELECTOR_32)
                    mstore(add(emptyPtr, 0x4), and(rawPair, ADDRESS_MASK))
                    mstore(add(emptyPtr, 0x24), srcAmount)
                    if iszero(call(gas(), WETH, 0, emptyPtr, 0x44, 0, 0)) {
                        reRevert()
                    }
                }
                default {
                    if callvalue() {
                        revertWithReason(0x00000011696e76616c6964206d73672e76616c75650000000000000000000000, 0x55) // "invalid msg.value"
                    }

                    if claim {
                        mstore(emptyPtr, TRANSFER_FROM_CALL_SELECTOR_32)
                        mstore(add(emptyPtr, 0x4), swapCaller)
                        mstore(add(emptyPtr, 0x24), and(rawPair, ADDRESS_MASK))
                        mstore(add(emptyPtr, 0x44), srcAmount)
                        if iszero(call(gas(), token, 0, emptyPtr, 0x64, 0, 0)) {
                            reRevert()
                        }
                    }
                }

                ret := srcAmount

                for {
                    let i := add(poolsOffset, 0x20)
                } lt(i, poolsEndOffset) {
                    i := add(i, 0x20)
                } {
                    let nextRawPair := calldataload(i)

                    ret := swap(
                        emptyPtr,
                        ret,
                        and(rawPair, ADDRESS_MASK),
                        and(rawPair, REVERSE_MASK),
                        shr(NUMERATOR_OFFSET, and(rawPair, NUMERATOR_MASK)),
                        and(nextRawPair, ADDRESS_MASK)
                    )

                    rawPair := nextRawPair
                }

                ret := swap(
                    emptyPtr,
                    ret,
                    and(rawPair, ADDRESS_MASK),
                    and(rawPair, REVERSE_MASK),
                    shr(NUMERATOR_OFFSET, and(rawPair, NUMERATOR_MASK)),
                    address()
                )

                if lt(ret, min) {
                    revertWithReason(0x000000164d696e2072657475726e206e6f742072656163686564000000000000, 0x5a) // "Min return not reached"
                }

                mstore(emptyPtr, 0xd21220a700000000000000000000000000000000000000000000000000000000)
                if and(rawPair, REVERSE_MASK) {
                    mstore(emptyPtr, 0x0dfe168100000000000000000000000000000000000000000000000000000000)
                }
                if iszero(staticcall(gas(), and(rawPair, ADDRESS_MASK), emptyPtr, 0x4, emptyPtr, 0x40)) {
                    reRevert()
                }
                let dstToken := mload(emptyPtr)

                let finalAmount := div(
                    mul(calldataload(0x44), 0x2710),
                    sub(
                        10000,
                        shr(
                            232,
                            and(
                                calldataload(add(add(calldataload(0x64), 0x4), 0x20)),
                                0x00ffff0000000000000000000000000000000000000000000000000000000000
                            )
                        )
                    )
                )
                switch gt(ret, finalAmount)
                case 1 {
                    switch and(rawPair, WETH_MASK)
                    case 0 {
                        mstore(emptyPtr, ERC20_TRANSFER_CALL_SELECTOR_32)
                        mstore(add(emptyPtr, 0x4), receiver)
                        mstore(add(emptyPtr, 0x24), finalAmount)
                        if iszero(call(gas(), dstToken, 0, emptyPtr, 0x44, 0, 0)) {
                            reRevert()
                        }

                        mstore(add(emptyPtr, 0x4), 0x922164BBBd36Acf9E854AcBbF32faCC949fCAEef)
                        mstore(add(emptyPtr, 0x24), sub(ret, finalAmount))
                        if iszero(call(gas(), dstToken, 0, emptyPtr, 0x44, 0, 0)) {
                            reRevert()
                        }
                    }
                    default {
                        mstore(emptyPtr, WETH_WITHDRAW_CALL_SELECTOR_32)
                        mstore(add(emptyPtr, 0x04), ret)
                        if iszero(call(gas(), WETH, 0, emptyPtr, 0x24, 0, 0)) {
                            reRevert()
                        }

                        if iszero(call(gas(), receiver, finalAmount, 0, 0, 0, 0)) {
                            reRevert()
                        }

                        if iszero(call(gas(), 0x922164BBBd36Acf9E854AcBbF32faCC949fCAEef, sub(ret, finalAmount), 0, 0, 0, 0)) {
                            reRevert()
                        }
                    }
                }
                default {
                    switch and(rawPair, WETH_MASK)
                    case 0 {
                        mstore(emptyPtr, ERC20_TRANSFER_CALL_SELECTOR_32)
                        mstore(add(emptyPtr, 0x4), receiver)
                        mstore(add(emptyPtr, 0x24), ret)
                        if iszero(call(gas(), dstToken, 0, emptyPtr, 0x44, 0, 0)) {
                            reRevert()
                        }
                    }
                    default {
                        mstore(emptyPtr, WETH_WITHDRAW_CALL_SELECTOR_32)
                        mstore(add(emptyPtr, 0x04), ret)
                        if iszero(call(gas(), WETH, 0, emptyPtr, 0x24, 0, 0)) {
                            reRevert()
                        }

                        if iszero(call(gas(), receiver, ret, 0, 0, 0, 0)) {
                            reRevert()
                        }
                    }
                }
            }

            let emptyPtr := mload(0x40)
            mstore(0x40, add(emptyPtr, 0xc0))
            returnAmount := callSwap(emptyPtr, srcToken, amount, caller(), recipient, minReturn, eq(claimed, 0))
        }
    }
}
